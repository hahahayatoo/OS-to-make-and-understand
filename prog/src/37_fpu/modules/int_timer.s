int_timer:
        ;---------------------------------------
        ; 【レジスタの保存】
        ;---------------------------------------
        pusha                                   ; スタックに全ての汎用レジスタのデータをプッシュ
        push    ds
        push    es

        ;---------------------------------------
        ; データ用セグメントセレクタの設定
        ;---------------------------------------
        mov     ax, 0x0010                      ; GDTの先頭からのバイト数
        mov     ds, ax
        mov     es, ax

        ;---------------------------------------
        ; TICK
        ;---------------------------------------
        inc     dword [TIMER_COUNT]             ; TIMER_COUNT++ 割り込み回数の更新

        ;---------------------------------------
        ; 割り込み終了コマンドの送信
        ;---------------------------------------
        outp    0x20, 0x20                      ; マスタPICにEOIコマンド送信

        ;---------------------------------------
        ; タスクの切り替え
        ;---------------------------------------
        str     ax                              ; AX = TR 現在のタスクレジスタ
        cmp     ax, SS_TASK_0                   ; 現在のタスクによってタスクを切り替える
        je      .11L                            ;  case SS_TASK_0: タスク1に切り替え
        cmp     ax, SS_TASK_1                   ;  case SS_TASK_1: タスク2に切り替え
        je      .12L                            ;         default: タスク0に切り替え

        jmp     SS_TASK_0:0
        jmp     .10E

.11L:
        jmp     SS_TASK_1:0
        jmp     .10E

.12L:
        jmp     SS_TASK_2:0
        jmp     .10E

.10E:
        ;---------------------------------------
        ; 【レジスタの復帰】
        ;---------------------------------------
        pop     es
        pop     ds
        popa

        iret                                    ; 割り込み処理の終了

ALIGN   4, dd 0
TIMER_COUNT:    dq 0