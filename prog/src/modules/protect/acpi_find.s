acpi_find:
        ;---------------------------------------
        ; 【スタックフレームの構築】
        ;---------------------------------------
                                                ; EBP + 16| テーブル名
                                                ; EBP + 12| サイズ
                                                ; EBP +  8| アドレス
                                                ; EBP +  4| EIP（戻り番地）
        push    ebp                             ; EBP +  0| EBP（元の値）
        mov     ebp, esp                        ; ------------------------

        ;---------------------------------------
        ; 【レジスタの保存】
        ;---------------------------------------
        push    ebx
        push    ecx
        push    edi

        ;---------------------------------------
        ; 引数を取得
        ;---------------------------------------
        mov     edi, [ebp + 8]                  ; EDI = アドレス
        mov     ecx, [ebp + 12]                 ; ECX = サイズ
        mov     eax, [ebp + 16]                 ; EAX = テーブル名

        ;---------------------------------------
        ; 名前の検索
        ;---------------------------------------
        cld                                     ; DFクリア
.10L:
        repne   scasb                           ; while( AL != *EDI) EDI++

        cmp     ecx, 0                          ;
        jnz     .11E                            ;
        mov     eax, 0                          ;
        jmp     .10E                            ; 一致するデータが見つからなければ戻り値に 0 を設定

.11E:
        cmp     eax, [es:edi - 1]               ; EAX != *EDI 4文字不一致?
        jne     .10L                            ; 不一致

        dec     edi                             ;
        mov     eax, edi                        ; 一致した場合は戻り値に検索位置のアドレスを設定

.10E:
        ;---------------------------------------
        ; 【レジスタの復帰】
        ;---------------------------------------
        pop     edi
        pop     ecx
        pop     ebx

        ;---------------------------------------
        ; 【スタックフレームの破棄】
        ;---------------------------------------
        mov     esp, ebp
        pop     ebp

        ret