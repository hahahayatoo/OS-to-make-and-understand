draw_font:
        ;---------------------------------------
        ; 【スタックフレームの構築】
        ;---------------------------------------
                                                ; EBP + 12| 行
                                                ; EBP +  8| 列
                                                ; EBP +  4| IP（戻り番地）
        push    ebp                             ; EBP +  0| BP（元の値）
        mov     ebp, esp                        ; ------|-----------

        ;---------------------------------------
        ; 【レジスタの保存】
        ;---------------------------------------
        push    eax
        push    ebx
        push    ecx
        push    edx
        push    esi
        push    edi

        ;---------------------------------------
        ; 表示位置の基準となる位置を保存
        ;---------------------------------------
        mov     esi, [ebp + 8]                  ; ESI = X(列)
        mov     edi, [ebp + 12]                 ; EDI = Y(行)

        ;---------------------------------------
        ; フォントの表示
        ;---------------------------------------
        mov     ecx, 0                          ; 表示する文字数（兼文字コード）

.10L:   cmp     ecx, 256
        jae     .10E

        mov     eax, ecx                        ;
        and     eax, 0x0F                       ; ECXの下位4ビットのみ取得したものに
        add     eax, esi                        ; X(列)を加算して桁位置を算出

        mov     ebx, ecx                        ;
        shr     ebx, 4                          ; 行位置は16文字出力毎に1行加算されるので
        add     ebx, edi                        ; ECXを16で割った値にY(行)を加算して行位置を算出

        cdecl   draw_char, eax, ebx, 0x07, ecx

        inc     ecx                             ;
        jmp     .10L

.10E:

        ;---------------------------------------
        ; 【レジスタの復帰】
        ;---------------------------------------
         pop    edi
         pop    esi
         pop    edx
         pop    ecx
         pop    ebx
         pop    eax

        ;---------------------------------------
        ; 【スタックフレームの破棄】
        ;---------------------------------------
        mov     esp, ebp
        pop     ebp

        ret