ctrl_alt_end:
        ;---------------------------------------
        ; 【スタックフレームの構築】
        ;---------------------------------------
                                                ; EBP +  8| キーコード
                                                ; EBP +  4| EIP（戻り番地）
        push    ebp                             ; EBP +  0| EBP（元の値）
        mov     ebp, esp                        ; ------------------------

        ;---------------------------------------
        ; キー状態保存
        ;---------------------------------------
        mov     eax, [ebp + 8]                  ; EAX = キーコード
        btr     eax, 7                          ; キーコードの最上位ビットをテストし、最上位ビットをクリア
        jc      .10F                            ; ブレイクコードが E0 のため、B7 がセットされていればキーを離されたと判断？
        bts     [.key_state], eax               ; EAX番目の.key_stateのビットをセット
        jmp     .10E

.10F:                                           ;
        btr     [.key_state], eax               ; EAX番目の.key_stateのビットをクリア

.10E:
        ;---------------------------------------
        ; キー押下判定
        ;---------------------------------------
        mov     eax, 0x1D                       ;
        bt      [.key_state], eax               ; [Ctrl]キーが押されているか？
        jnc     .20E                            ;

        mov     eax, 0x38                       ;
        bt      [.key_state], eax               ; [Alt]キーが押されているか？
        jnc     .20E                            ;

        mov     eax, 0x4F                       ;
        bt      [.key_state], eax               ; [End]キーが押されているか？
        jnc     .20E                            ;

        mov     eax, -1                         ; ret = -1

.20E:
        sar     eax, 8                          ; ret >>= 8 最上位ビットは0にしているので0が返る

        ;---------------------------------------
        ; 【スタックフレームの破棄】
        ;---------------------------------------
        mov     esp, ebp
        pop     ebp

        ret

.key_state: times 32 db 0                       ; 32バイト（256ビット）分のメモリ確保