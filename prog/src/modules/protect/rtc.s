rtc_get_time:
        ;---------------------------------------
        ; 【スタックフレームの構築】
        ;---------------------------------------
                                                ; EBP +  8| 保存先アドレス
                                                ; --------------------
        push    ebp                             ; EBP +  4| EIP（戻り番地）
        mov     ebp, esp                        ; EBP +  0| EBP（元の値）
                                                ; --------------------

        ;---------------------------------------
        ; 【レジスタの保存】
        ;---------------------------------------
        push    ebx

        ;---------------------------------------
        ; RTCから時・分・秒データ取得
        ;---------------------------------------
        mov     al, 0x04                        ; RTCから時間データを取得するために
        out     0x70, al                        ; 0x70番地にRTC内蔵RAMアドレス 4 を書き込み、
        in      al, 0x71                        ; 0x71番地からデータを取得する

        shl     eax, 8                          ; 時間データを退避

        mov     al, 0x02                        ; RTCから分データを取得
        out     0x70, al
        in      al, 0x71

        shl     eax, 8

        mov     al, 0x00                        ; RTCから秒データを取得
        out     0x70, al
        in      al, 0x71

        and     eax, 0x00_FF_FF_FF              ; 時刻データが格納されているEAXの下位3バイトのみ有効化

        mov     ebx, [ebp + 8]                  ; dst = 保存先
        mov     [ebx], eax                      ; [dst] = 時刻

        ;---------------------------------------
        ; 【レジスタの復帰】
        ;---------------------------------------
         pop    ebx

        ;---------------------------------------
        ; 【スタックフレームの破棄】
        ;---------------------------------------
        mov     esp, ebp
        pop     ebp

        ret