draw_pixel:
        ;---------------------------------------
        ; 【スタックフレームの構築】
        ;---------------------------------------
                                                ; EBP + 16| 描画色
                                                ; EBP + 12| Y座標
                                                ; EBP +  8| X座標
                                                ; EBP +  4| IP（戻り番地）
        push    ebp                             ; EBP +  0| BP（元の値）
        mov     ebp, esp                        ; --------|-----------

        ;---------------------------------------
        ; 【レジスタの保存】
        ;---------------------------------------
        push    eax
        push    ebx
        push    ecx
        push    edi

        ;---------------------------------------
        ; Y座標を80倍する（640/8）
        ;---------------------------------------
        mov     edi, [ebp + 12]                 ; 640ドットなのでY座標を80倍した値が
        shl     edi, 4                          ; VRAMの開始アドレスになる
        lea     edi, [edi * 4 + edi + 0xA_0000] ;

        ;---------------------------------------
        ; X座標を1/8して加算
        ;---------------------------------------
        mov     ebx, [ebp + 8]                  ; 1バイト単位でのメモリアクセスなので、
        mov     ecx, ebx                        ; 渡されたX座標の値を1/8したものを
        shr     ebx, 3                          ; 加算することでVRAMアドレスを決定
        add     edi, ebx                        ;

        ;---------------------------------------
        ; X座標を8で割った余りからビット位置を計算
        ; (0=0x80, 1=0x40, ..., 7=0x01)
        ;---------------------------------------
        and     ecx, 0x07
        mov     ebx, 0x80
        shr     ebx, cl

        ;---------------------------------------
        ; 色指定
        ;---------------------------------------
        mov     ecx, [ebp + 16]

        ;---------------------------------------
        ; プレーンごとに出力
        ;---------------------------------------
        cdecl   vga_set_read_plane, 0x03        ; 書き込みプレーン：輝度
        cdecl   vga_set_write_plane, 0x08       ; 読み込みプレーン：輝度
        cdecl   vram_bit_copy, ebx, edi, 0x08, ecx

        cdecl   vga_set_read_plane, 0x02        ; 書き込みプレーン：赤
        cdecl   vga_set_write_plane, 0x04       ; 読み込みプレーン：赤
        cdecl   vram_bit_copy, ebx, edi, 0x04, ecx

        cdecl   vga_set_read_plane, 0x01        ; 書き込みプレーン：緑
        cdecl   vga_set_write_plane, 0x02       ; 読み込みプレーン：緑
        cdecl   vram_bit_copy, ebx, edi, 0x02, ecx

        cdecl   vga_set_read_plane, 0x00        ; 書き込みプレーン：青
        cdecl   vga_set_write_plane, 0x01       ; 読み込みプレーン：青
        cdecl   vram_bit_copy, ebx, edi, 0x01, ecx

        ;---------------------------------------
        ; 【レジスタの復帰】
        ;---------------------------------------
         pop    edi
         pop    ecx
         pop    ebx
         pop    eax

        ;---------------------------------------
        ; 【スタックフレームの破棄】
        ;---------------------------------------
        mov     esp, ebp
        pop     ebp

        ret